fn formatSkeleton obj stream =
(
	objSkin = obj.modifiers[#skin]
	if (objSkin != undefined) then
	(
		allBones = skinOps.GetBoneNodes objSkin
		bonesCount = allBones.count
		if (bonesCount > 0) then
		(
			format "%\"%\":%[%" (tabs 2) obj.name sp nl to:stream

			for bi = 1 to bonesCount do
			(
				b = allBones[bi]
				
				localTransform = b.transform
				
				parentBoneID = -1
				if (b.parent != undefined) then
				(	
					for bi2 = 1 to bonesCount do
					(
						if (b.parent == allBones[bi2]) then
						(
							parentBoneID = bi2 - 1
							break
						)
					)
					
					localTransform *= inverse b.parent.transform
				)
				else
				(
					localTransform *= inverse obj.transform
				)
				
				localRotation = localTransform.rotation
				
				if flipAxis then
				(
					localTransform *= flipAxisMatrix
					localRotation *= flipAxisMatrix
				)
				
				format "%{%\"nm\":\"%\"" (tabs 3) sp b.name to:stream
				
				if (exportSkinBoneParent) then
				(
					format ",%\"pr\":%" sp parentBoneID to:stream
				)
				
				if (exportSkinBonePosition) then
				(
					format ",%\"ps\":%" sp (formatPoint3 localTransform.position) to:stream
				)
				
				if (exportSkinBoneRotation) then
				(
					-- Inverse the rotation because : "Internally, 3ds Max stores rotations in node matrices using the left-hand rule."
					-- Cf. Maxscript doc : Using Node Transform Properties (https://help.autodesk.com/view/MAXDEV/2022/ENU/?guid=GUID-3B001F21-8FE9-4663-A972-E648682A0ACD)
					format ",%\"rt\":%" sp (formatQuaternion (inverse localRotation)) to:stream
				)
				
				if (exportSkinBoneScale) then
				(
					format ",%\"sc\":%" sp (formatPoint3 localTransform.scale) to:stream
				)
				
				format "%}%%" sp (if bi != bonesCount then "," else "") nl to:stream
			)
			
			format "%]%" (tabs 2) nl to:stream
		)
	)
)
